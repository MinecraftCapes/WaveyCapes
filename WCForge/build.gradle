project.evaluationDependsOn(':WCShared')

archivesBaseName = rootProject.archives_base_name + "-forge"

dependencies {
    compileOnly project(':WCShared')

    forge "net.minecraftforge:forge:${rootProject.minecraft_version}-${rootProject.forge_version}"

    //MinecraftCapes and Auth for testing
    modImplementation "curse.maven:minecraftcapes-mod-359836:3574424"
    modImplementation "curse.maven:oauth-513857:3587914"
}

processResources {
    from project(":WCShared").sourceSets.main.resources

    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

loom {
    forge.mixinConfigs "${modid}.mixins.json"
}

tasks.withType(JavaCompile) {
    source(project(":WCShared").sourceSets.main.allSource)
}

jar {
    manifest {
        attributes([
                "Specification-Title": "${rootProject.modid}",
                "Specification-Vendor": "${rootProject.author}",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": "${version}",
                "Implementation-Vendor" :"${rootProject.author}",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs": "${rootProject.modid}.mixins.json"
        ])
    }
}